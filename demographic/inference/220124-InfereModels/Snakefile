'''
Demographic inference pipeline
'''

### *************************************************** ####
### ************* MODEL INFERENCE ********************* ####
### *************************************************** ####

def get_mL_file(wildcards):
    if wildcards.varcat == 'intronic':
        return '../../data/220113-ConstructBoostrapedDatasets/data/whole-genome/mL-introns.txt'
    else:
        return '../../data/220113-ConstructBoostrapedDatasets/data/whole-genome/mL-' + wildcards.varcat + '.txt'


rule infer_OOA:
    input:
        model = 'data/OOA-model.yml',
        parameters = 'data/OOA-parameters.yml',
        mL = get_mL_file,
        data = '../../data/220113-ConstructBoostrapedDatasets/data/whole-genome/spectrum-cat_{varcat}.pkl.gz'
    output:
        'results/best-guest-OOA-{varcat}.yml',
        'results/best-guest-OOA-{varcat}-bestparameters.csv'
    params:
        outprefix = 'results/best-guest-OOA-{varcat}'
    shell:
        '''
        python scripts/infere-ooa-model.py \
            {input.model} {input.parameters} \
            {input.mL} {input.data} \
            {params.outprefix}
        '''


rule make_NAT_EXPANSION_FILES:
    input:
        ooa_best = 'results/best-guest-OOA-{varcat}.yml',
        nat_specific_mdl = 'data/NAT-EXPANSION-files/NatExpansion-params.txt',
        nat_parameters = 'data/NAT-EXPANSION-files/NatExpansion-options.yml'
    output:
        model = 'data/NAT-EXPANSION-MODELS/NAT-EXPANSION-model-{varcat}.yml',
        parameters = 'data/NAT-EXPANSION-MODELS/NAT-EXPANSION-parameters-{varcat}.yml'
    shell:
        '''
		#Make model yml file
		sed -n "1,35p" {input.ooa_best} >{output.model}
		cat {input.nat_specific_mdl} >>{output.model}
		sed -n "36,44p" {input.ooa_best} >>{output.model}
		#Make params file
		sed -n "1,3p" {input.nat_parameters} >{output.parameters} 
		python scripts/get-CHB-start-time.py {input.ooa_best} >>{output.parameters}
		sed -n "5,21p" {input.nat_parameters} >>{output.parameters}
        '''


rule infer_NAT_EXPANSION:
    input:
        model = 'data/NAT-EXPANSION-MODELS/NAT-EXPANSION-model-{varcat}.yml',
        parameters = 'data/NAT-EXPANSION-MODELS/NAT-EXPANSION-parameters-{varcat}.yml',
        mL = get_mL_file,
        data = '../../data/220113-ConstructBoostrapedDatasets/data/whole-genome/spectrum-cat_{varcat}.pkl.gz'
    output:
        'results/best-guest-NAT-EXPANSION-{varcat}.yml',
        'results/best-guest-NAT-EXPANSION-{varcat}-bestparameters.csv'
    params:
        outprefix = 'results/best-guest-NAT-EXPANSION-{varcat}'
    shell:
        '''
        python scripts/infere-NAT-EXPANSION-model.py \
            {input.model} {input.parameters} \
            {input.mL} {input.data} \
            {params.outprefix}
        '''


### *************************************************** ####
### ************* MODEL UNCERTAINTY ******************* ####
### *************************************************** ####


rule put_genome_chunks_in_file:
    # I have a file for each genome chunk
    # here i put them together in a python list object
    input:
        sfs_path = directory('../../data/220113-ConstructBoostrapedDatasets/data/jSFS/spectrums/'),
        mL_path = directory('../../data/220113-ConstructBoostrapedDatasets/data/mL-noncoding/mLs/')

    output:
        'data/boostrap-replicates/{varcat}-GENOME-CHUNKS.pkl.gz'
    log: 'logs/{varcat}-GENOME-CHUNKS.log'
    threads: 20
    shell:
        """
        python scripts/put-genome-chunks-in-list.py \
            {input.sfs_path} {input.mL_path} \
            {wildcards.varcat} {threads} {output} 2>{log}
        """


rule get_bootstrap_replicates:
    input:
        'data/boostrap-replicates/{varcat}-GENOME-CHUNKS.pkl.gz'
    output:
        'data/boostrap-replicates/{varcat}-BOOTSTRAPS.pkl.gz'
    log: 'logs/{varcat}-BOOTSTRAPS.log'
    threads: 20
    shell:
        """
        python scripts/get-bootstrap-replicates.py \
            {input} {output} {threads} 2>{log}
        """

### helper function to define input files


def get_option_for_mdl(wildcards):
    if wildcards.mdl == 'OOA':
        return 'data/OOA-parameters.yml'
    else:
        #Â NAT expansion mdl
        return 'data/NAT-EXPANSION-MODELS/NAT-EXPANSION-parameters-{varcat}.yml'

def marginalize_nat(wildcards):
    if wildcards.mdl == 'OOA':
        return 'yes'
    else:
        return 'no'

rule estimate_cofidence:
    input:
        data = '../../data/220113-ConstructBoostrapedDatasets/data/whole-genome/spectrum-cat_{varcat}.pkl.gz',
        mL = get_mL_file,
        bootstraps = 'data/boostrap-replicates/{varcat}-BOOTSTRAPS.pkl.gz',
        optimized_model = 'results/best-guest-{mdl}-{varcat}.yml',
        parameters = get_option_for_mdl
    output:
        'results/ConfidenceIntervals/{mdl}-{varcat}.tsv'
    params:
        marginalize = marginalize_nat,
        project_size = 30
    shell:
        '''
        python scripts/Estimate-CI.py \
            {input.data} {input.mL} \
            {input.bootstraps} \
            {params.project_size} {params.marginalize} \
            {input.optimized_model} {input.parameters} \
            {output}
        '''



