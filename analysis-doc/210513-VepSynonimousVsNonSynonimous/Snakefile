"""
Compute the SFS for synonymous and nonsynonymous variants

NOTES:
    - To run this pipeline you need to activate the popgene conda environment:
        conda activate popgene
    - Also, I use the bcftools pluggin split-vep: https://samtools.github.io/bcftools/howtos/plugin.split-vep.html
        The varible BCFTOOLS_PLUGINS should point to the correct path.
"""


# I use a regular expression with the variant consequences I want
# to retrieve, see: https://m.ensembl.org/info/genome/variation/prediction/predicted_data.html

SYNONYMOUS = '(synonymous_variant|stop_retained_variant|start_retained_variant)'
NON_SYNONYMOUS = '(missense_variant|inframe_deletion|inframe_insertion|frameshift_variant|stop_gained|coding_sequence_variant)'
CHROMOSOMES = range(1, 23)


rule concat_vcfs:
    input:
        expand("../../results/data/210305-merged-with-1TGP/strict-mask/1TGP_and_50MXB-chr{chrn}-snps-vep-mask-GRCh38.vcf.gz", chrn=CHROMOSOMES)
    output:
        temp("data/all-vcf.gz")
    shell:
        """
        bcftools concat {input} -Oz -o {output}
        """


rule concat_ancestral_allel:
    input:
        expand("../210506-AncestralAlleleData/data/aa-chr{chrn}.csv", chrn=CHROMOSOMES)
    output:
        temp("data/all-ancestral-allel.csv")
    shell:
        """
        awk '(NR == 1) || (FNR > 1)' {input} >{output}
        """


rule biallelic_snps:
    input:
        "data/all-vcf.gz"
    output:
        temp("data/all-biallelicSnps-vcf.gz")
    shell:
        """
        bcftools view -m2 -M2 -v snps {input} -Oz -o {output}
        """

rule var_id_to_vep_consequences:
    input:
        # A vcf that has VEP annotation in the INFO field
        "data/all-biallelicSnps-vcf.gz"
    output:
        "data/variants-csq.txt"
    shell:
        # The -d option in the bcftools command is to
        # To print each consequence on a separate line,
        # rather than as a comma-separated string on a single line.
        # This will make easier the task processing
        """
        bcftools +split-vep {input} -f '%ID\t%Consequence\n' -d >{output}
        """


rule subset_synonymous:
    input:
        "data/variants-csq.txt"
    output:
        temp("data/synonymous-vars.txt")
    shell:
        """
        grep -E '{SYNONYMOUS}' {input} |\
            cut -f1 |\
            sort |\
            uniq > {output}
        """


rule subset_nonsynonymous:
    input:
        "data/variants-csq.txt"
    output:
        temp("data/nonsynonymous-vars.txt")
    shell:
        """
        grep -E '{NON_SYNONYMOUS}' {input} |\
            cut -f1 |\
            sort |\
            uniq > {output}
        """


rule subset_vcf_vars:
    input:
        vcf = "data/all-biallelicSnps-vcf.gz",
        ids_type = "data/{type}-vars.txt"
    output:
        "data/vcf-{type}.vcf.gz"
    shell:
        """
        bcftools view -i 'ID=@{input.ids_type}' {input.vcf} -Oz -o {output}
        """

rule sfs:
    input:
        "data/vcf-{type}.vcf.gz",
        "data/all-ancestral-allel.csv"
    output:
        "results/sfs-{type}.csv",
        "results/aa-stats-{type}.csv"
    shell:
        """
        python compute-sfs.py {input[0]} {input[1]} {output[0]} {output[1]}
        """


rule all:
    input:
        "results/sfs-synonymous.csv",
        "results/sfs-nonsynonymous.csv",
