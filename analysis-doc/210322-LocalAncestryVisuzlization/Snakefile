
# where is this dir located ?
# https://github.com/santiago1234/popgene-utils

from os.path import join
import os
import pandas as pd
import sys
sys.path.append('../../')
from mxbgenomes.utils import load_1tgp_metada
from utils import subpops_and_samples  # see script: utils.py

CHROM = list(range(1, 23))
configfile: "config.yaml"

RESULTS_DIR = join('results', config['results_prefix'])
DATA_DIR = join('data', config['results_prefix'])
PLOTS_DIR = join('plots', config['results_prefix'])

# PATH_TO_POPGENE = "../../../../../Research/popgene-utils/"
# I assume that the msp file hsa the format mdl-{chrn}.msp.tsv
# This is the path to the directory containing the msp files
# for all the chromosomes
PATH_TO_XGMIX = config['PATH_TO_XGMIX']
subpops = ['MXL', 'PEL', 'PUR', 'CLM']
ONETGP = load_1tgp_metada('../../resources/1TGP-samples-meta-data/integrated_call_samples_v3.20130502.ALL.panel', filter_subpops=True)
ONETGP = ONETGP[ONETGP.Subpopulation.isin(subpops)]
SAMPLES = ONETGP.Samplename.to_list()

# rule compute_global_ancestry_from_local:
#     input:
#         "../../results/data/210308-local-ancestry/predictions/mdl-{chrn}.msp.tsv"
#     output:
#         "data/local-chrn{chrn}.csv"
#     params:
#         chrn = "{chrn}",
#         rscript = join(PATH_TO_POPGENE, "scripts/global-ancestry-xgmix.R")
#     shell:
#         """
#         Rscript {params.rscript} \
#             -m {input} -c {params.chrn} -o {output}
#         """

rule msp_to_bed:
    input:
        join(PATH_TO_XGMIX, "mdl-{chrn}.msp.tsv")
    output:
        temp(join(DATA_DIR, "bed/chr{chrn}-local-ancestry-{individual}.tsv"))
    params:
        individual = '{individual}',
    shell:
        """
        python scripts/msp_to_bed.py -msp {input} -ind {params.individual} -out {output}
        """

rule merge_msp:
    input:
        expand(join(DATA_DIR, "bed/chr{chrn}-local-ancestry-{{individual}}.tsv"), chrn=CHROM)
    output:
        join(DATA_DIR, "bed/allchrn-{individual}.tsv")
    shell:
        """
        #Add the header
        head -n1 {input[0]} >{output}
        for file in {input}
        do
            tail -n+2 $file >>{output}
        done
        """


rule plot_cariotipo:
    input:
        join(DATA_DIR, "bed/allchrn-{individual}.tsv")
    output:
        join(PLOTS_DIR, "karyo-{individual}.png")
    script: "scripts/karyotype-local-ancestry-plot.R"


rule compute_tract_len_distribution:
    input:
        join(DATA_DIR, "bed/allchrn-{individual}.tsv")
    output:
        join(RESULTS_DIR, "tracts/{individual}-tracts.csv")
    shell:
        """
        python scripts/tract_lenght.py -bed {input} -out {output}
        """


rule tract_dist_over_subpopulation:
    """
    tract distribution averaged over individuals
    from the populations.
    """
    input:
       expand(join(RESULTS_DIR, "tracts/{individual}-tracts.csv"), individual=SAMPLES)
    output:
        join(RESULTS_DIR, "tract-distribution-by-population.csv")
    params:
        path_to_tracts = join(RESULTS_DIR, "tracts")
    shell:
        """
        # See the script for additional info
        python scripts/average_tract_dist_subpops.py \
            -tracts {params.path_to_tracts} \
            -panel ../../resources/1TGP-samples-meta-data/integrated_call_samples_v3.20130502.ALL.panel \
            -out {output}
        """

